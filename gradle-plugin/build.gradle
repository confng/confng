plugins {
    id 'java-gradle-plugin'
    id 'maven-publish'
    id 'signing'
}

group = 'org.confng'
version = '1.1.1'

java {
    sourceCompatibility = JavaVersion.VERSION_11
    targetCompatibility = JavaVersion.VERSION_11
    withJavadocJar()
    withSourcesJar()
}

repositories {
    mavenCentral()
}

dependencies {
    testImplementation 'org.junit.jupiter:junit-jupiter:5.10.2'
    testRuntimeOnly 'org.junit.platform:junit-platform-launcher'
}

test {
    useJUnitPlatform()
}

// Functional tests
sourceSets {
    functionalTest {
        java.srcDir 'src/functionalTest/java'
        resources.srcDir 'src/functionalTest/resources'
        compileClasspath += sourceSets.main.output
        runtimeClasspath += sourceSets.main.output
    }
}

configurations {
    functionalTestImplementation.extendsFrom testImplementation
    functionalTestRuntimeOnly.extendsFrom testRuntimeOnly
}

tasks.register('functionalTest', Test) {
    description = 'Runs the functional tests.'
    group = 'verification'
    testClassesDirs = sourceSets.functionalTest.output.classesDirs
    classpath = sourceSets.functionalTest.runtimeClasspath
    useJUnitPlatform()
    mustRunAfter tasks.test
}

tasks.named('check') {
    dependsOn tasks.functionalTest
}

gradlePlugin.testSourceSets.add(sourceSets.functionalTest)

gradlePlugin {
    plugins {
        confngPlugin {
            id = 'org.confng'
            implementationClass = 'org.confng.gradle.ConfNGPlugin'
            displayName = 'ConfNG Gradle Plugin'
            description = 'Automatically forwards system properties to test JVM for ConfNG configuration'
        }
    }
}

publishing {
    publications.withType(MavenPublication).configureEach {
        pom {
            name.set("ConfNG Gradle Plugin")
            description.set("Gradle plugin that automatically forwards system properties to test JVM")
            url.set("https://confng.org")
            licenses {
                license {
                    name.set("Apache-2.0")
                    url.set("https://www.apache.org/licenses/LICENSE-2.0")
                }
            }
            developers {
                developer {
                    id.set("confng")
                    name.set("ConfNG Team")
                    url.set("https://github.com/confng")
                }
            }
            scm {
                connection.set("scm:git:https://github.com/confng/confng.git")
                developerConnection.set("scm:git:ssh://github.com/confng/confng.git")
                url.set("https://github.com/confng/confng")
            }
        }
    }
    repositories {
        maven {
            name = "staging"
            // Use root project's staging directory so JReleaser picks it up
            url = rootProject.layout.buildDirectory.dir("staging-deploy").get().asFile.toURI()
        }
    }
}

signing {
    required { false }  // JReleaser handles signing
}

